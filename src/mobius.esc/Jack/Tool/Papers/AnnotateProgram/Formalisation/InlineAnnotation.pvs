% Step 3 of translation: inlining of method level set annotations into
% method body

InlineVariableNames[Name : TYPE+] : THEORY
BEGIN

  ASSUMING

    % Name space is infinite

    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

  ENDASSUMING

  IMPORTING WellFormedProgram[Name]

  % New variables cannot have name that is already used in program or 
  % reserved word
  legal_names(p : Program)(n : Name) : bool =
    NOT union(declared_var_names(p), reserved_words)(n)


END InlineVariableNames


InlineNewVariableDeclarations[Name : TYPE+] : THEORY
BEGIN

  ASSUMING

    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

  IMPORTING InlineVariableNames[Name]

    % for every program p, method m, a unique legal name can be found
    name_of_exists : ASSUMPTION
      EXISTS (f : [p : Program -> [Method -> (legal_names(p))]]) :
        FORALL(p : Program) :
          injective?(f(p))

  ENDASSUMING

  % name of new local variable res(p)(m) for each method m
  res(p : Program) : (injective?[Method, (legal_names(p))])


  p : VAR Program
  m : VAR Method


  % Variable that will be added by the inline process

  new_res_lvar(p, m) : Decl =
    (# name := res(p)(m),
       jtype := res_type(m),
       init_val := default_value(res_type(m)) #)

  % New variables will be local variables
  new_lvars(p) : finite_set[Decl] =
    {lv : Decl | EXISTS (m : Method) : 
                   methods(p)(m) AND lv = new_res_lvar(p, m) }

  new_res_lvar_new_lvar : LEMMA % :-)
     methods(p)(m) IMPLIES
     new_lvars(p)(new_res_lvar(p, m))


  IMPORTING ProgramFunctions[Name], SetProp[Name]

  % new local variable disjoint from:
  % existing field, ghost or local variable declarations
  % and reserved words

  new_lvars_not_field_decls : LEMMA % :-)
     disjoint?(var_names(new_lvars(p)), var_names(getFieldDecls(p)))
 
  new_lvars_not_ghost_decls : LEMMA % :-)
     disjoint?(var_names(new_lvars(p)), var_names(getGhostDecls(p)))

  new_lvars_not_old_local_decls : LEMMA % :-)
     disjoint?(var_names(new_lvars(p)), var_names(getLocalDecls(p)))

  new_lvars_not_reserved_words : LEMMA % :-)
     disjoint?(var_names(new_lvars(p)), reserved_words)

  new_lvars_not_ext_declared_var_names : LEMMA % :-)
     disjoint?(var_names(new_lvars(p)), ext_declared_var_names(p))


END InlineNewVariableDeclarations


InlineMethodAnnotation[Name : TYPE+] : THEORY
BEGIN

  % Inline Method level set-annotations into method body

  ASSUMING


    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})


  IMPORTING InlineVariableNames[Name]

    name_of_exists : ASSUMPTION
      EXISTS (f : [p : Program -> [Method -> (legal_names(p))]]) :
        FORALL(p : Program) :
          injective?(f(p))


  ENDASSUMING


  IMPORTING InlineNewVariableDeclarations[Name]

  p : VAR Program
  m : VAR Method

  % several try-catches needed, because we do not model subclass hierarchy
  % of exceptions

  % inline_method_body(p)(m) gives in Java-like syntax:
  % try{
  %   try{
  %     pre_set(m);
  %     body(m); 
  %     res(p)(m) := result(m);
  %     post_set(m);
  %   }
  %   catch(Throwable){
  %     exc_set(m);
  %   }
  % catch(NullPointerExc){
  %   exc_set(m);
  % }
  inline_method_body(p)(m) : Body = 
    TryCatch(
       TryCatch(
        Sequence(pre_set(m)(var_lookup(name(arg(m)))(jtype(arg(m)))),
        Sequence(body(m),
        Sequence(StmtExpr(Assign(res(p)(m), res(m))),
                 % assignment and post_set are only evaluated if
                 % body + res terminate normally
                 post_set(m)(var_lookup(res(p)(m))(res_type(m)))))),
         Throwable,
         Sequence(exc_set(m)(Throwable),
                  Throw(Throwable)),
         Skip),
       NullPointer,
       Sequence(exc_set(m)(NullPointer),
                Throw(NullPointer)),
       Skip)
         
  % inline method: pre_set, post_set and exc_set set to Skip
  % new result variable res(p)(m) added as local variable for this method
  % body becomes inline_method_body
  % new result expression: lookup of new local variable res(p)(m)
  inline_method(p)(m : Method) : Method =
    m WITH [pre_set  := LAMBDA(v : Expr) : Skip,
            post_set := LAMBDA(v : Expr) : Skip,
            exc_set := LAMBDA (e : Excpt) : Skip,
            lvars := add(new_res_lvar(p, m),lvars(m)),
            body := inline_method_body(p)(m),
            res := var_lookup(res(p)(m))(res_type(m))]

  % inline each method in ms
  inline_methods(p)(ms : finite_set[Method]) : finite_set[Method] =
    {m : Method | EXISTS (n : Method) : ms(n) AND m = inline_method(p)(n)}

  % simple rewrite lemmas, useful for auto-rewrites in proofs

  inline_method_name : LEMMA % :-)
    FORALL (m : Method) :
      name(inline_method(p)(m)) = name(m)

  inline_method_arg : LEMMA % :-)
    FORALL (m : Method) :
      arg(inline_method(p)(m)) = arg(m)

  inline_method_pre : LEMMA % :-)
    FORALL (m : Method) :
      pre(inline_method(p)(m)) = pre(m)

  inline_method_post : LEMMA % :-)
    FORALL (m : Method) :
      post(inline_method(p)(m)) = post(m)

  inline_method_lvars : LEMMA % :-)
    FORALL (m : Method) :
      lvars(inline_method(p)(m)) = 
        add(new_res_lvar(p, m), lvars(m))

  inline_method_body : LEMMA % :-)
    FORALL (m : Method) :
      body(inline_method(p)(m)) = inline_method_body(p)(m)

  inline_method_pre_set : LEMMA % :-)
    FORALL (m : Method) :
      pre_set(inline_method(p)(m)) = LAMBDA(v : Expr) : Skip

  inline_method_post_set : LEMMA % :-)
    FORALL (m : Method) :
      post_set(inline_method(p)(m)) = LAMBDA(v : Expr) : Skip

  inline_method_exc_set : LEMMA % :-)
    FORALL (m : Method) :
      exc_set(inline_method(p)(m)) = LAMBDA(e : Excpt) : Skip

  inline_method_res : LEMMA % TO DO
    FORALL (m : Method) :
      res(inline_method(p)(m)) = var_lookup(res(p)(m))(res_type(m))

  inline_method_res_type : LEMMA % :-)
    FORALL (m : Method) :
      res_type(inline_method(p)(m)) = res_type(m)

  % components of inline_method satisfy first conjunct of wf_Body
  inline_method_wf_Body1 : LEMMA % :-)
    FORALL(m : Method) :
      methods(p)(m) IMPLIES
      wf_Method(p)(m) IMPLIES
      LET im = inline_method(p)(m) IN
      (FORALL(v : Name) : var_names(body(im))(v) IMPLIES
                          ext_declared_var_names(p)(v) OR
                          var_names(new_lvars(p))(v))
      AND
      (FORALL(v : Name, e : Expr) : var_names(pre(im)(e))(v) IMPLIES
                          ext_declared_var_names(p)(v))
      AND
      (FORALL(v : Name, e : Expr) : var_names(post(im)(e))(v) IMPLIES
                          ext_declared_var_names(p)(v))
      AND
      (FORALL(v : Name) : var_names(res(im))(v) IMPLIES
                          var_names(new_lvars(p))(v))
      AND
      (FORALL(v : Name, e : Expr) : NOT var_names(pre_set(im)(e))(v))
      AND
      (FORALL(v : Name, e : Expr) : NOT var_names(post_set(im)(e))(v))
      AND
      (FORALL(v : Name, e : Excpt) : NOT var_names(exc_set(im)(e))(v))

  % components of inline_method satisfy second conjunct of wf_Body
  inline_method_wf_Body2 : LEMMA % :-)
    FORALL(m : Method, b : Body) :
      wf_Method(p)(m) IMPLIES
      Set?(b) IMPLIES
      LET im = inline_method(p)(m) IN
      (subterm(b, body(im)) IMPLIES
         var_names(getGhostDecls(p))(target(b))) AND
      (FORALL(e : Expr) : 
       subterm(b, pre(im)(e)) IMPLIES
         var_names(getGhostDecls(p))(target(b))) AND
      (FORALL(e : Expr) :
       subterm(b, post(im)(e)) IMPLIES
         var_names(getGhostDecls(p))(target(b))) AND
      (NOT subterm(b, res(im))) AND
      (FORALL(e : Expr) : NOT subterm(b, pre_set(im)(e))) AND
      (FORALL(e : Expr) : NOT subterm(b, post_set(im)(e))) AND
      (FORALL(e : Excpt) : NOT subterm(b, exc_set(im)(e)))


END InlineMethodAnnotation


InlineClassAnnotation[Name : TYPE+] : THEORY
BEGIN

  % Inline all methods in class

  ASSUMING


    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

  IMPORTING InlineVariableNames[Name]

    name_of_exists : ASSUMPTION
      EXISTS (f : [p : Program -> [Method -> (legal_names(p))]]) :
        FORALL(p : Program) :
          injective?(f(p))


  ENDASSUMING

  IMPORTING InlineMethodAnnotation[Name]

  p : VAR Program

  % apply inline_method on all methods in class
  % otherwise, class unchanged
  inline_class(p)(c : Class) : Class =
    c WITH [methods := inline_methods(p)(methods(c))]

  % simple rewrite lemmas, useful for auto-rewrites in proofs
  inv_inline_class : LEMMA
    FORALL(c : Class) :
      inv(inline_class(p)(c)) = inv(c)

  FieldDecls_inline_class : LEMMA % :-)
    FORALL(c : Class) :
      fields(inline_class(p)(c)) = fields(c)

  GhostDecls_inline_class : LEMMA % :-)
    FORALL(c : Class) :
      ghost_vars(inline_class(p)(c)) = ghost_vars(c)

  name_inline_class : LEMMA
    FORALL(c : Class) :
      name(inline_class(p)(c)) = name(c)

  % inlines_class satisfies global part of the wf_Class condition
  inline_class_almost_wf_Class : LEMMA % :-)
    FORALL(c : Class) :
      wf_Class(p)(c) IMPLIES
      LET ic = inline_class(p)(c) IN
        FORALL (m1, m2 : Method) :
           methods(ic)(m1) IMPLIES
           methods(ic)(m2) IMPLIES
           name(m1) = name(m2) IMPLIES
           m1 = m2

  % invariant in class satisfies first conjunct of wf_Body
  inline_class_inv_wf_Body1 : LEMMA % :-)
    FORALL(c : Class) :
      wf_Body(p)(inv(c)) IMPLIES
       (FORALL (v : Name) : var_names(inv(inline_class(p)(c)))(v) IMPLIES
                            ext_declared_var_names(p)(v))

  % invariant in class satisfies second conjunct of wf_Body
  inline_class_inv_wf_Body2 : LEMMA % :-)
    FORALL(c : Class) :
      wf_Body(p)(inv(c)) IMPLIES
         FORALL(b : Body) : 
           Set?(b) IMPLIES 
           subterm(b, inv(inline_class(p)(c))) IMPLIES 
            var_names(getGhostDecls(p))(target(b))



END InlineClassAnnotation


InlineProgramAnnotation[Name : TYPE+] : THEORY
BEGIN

  % Inline Method-level set annotations for whole program

  ASSUMING


    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

  IMPORTING InlineVariableNames[Name]


    name_of_exists : ASSUMPTION
      EXISTS (f : [p : Program -> [Method -> (legal_names(p))]]) :
        FORALL(p : Program) :
          injective?(f(p))


  ENDASSUMING

  IMPORTING InlineClassAnnotation[Name]

  p : VAR Program

  % Inline set-annotations into method bodies

  IMPORTING SetProp[Name] % to prove TCC

  % Apply inline_class to all classes in program
  inline_program(p) : Program =
    (# classes := {c : Class | EXISTS (c1 : Class) :
                                 classes(p)(c1) AND
                                 c = inline_class(p)(c1)} #)


END InlineProgramAnnotation



InlineProgramAnnotationProperties[Name : TYPE+] : THEORY
BEGIN

  % Properties about inlining algorithm

  ASSUMING

    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

  IMPORTING InlineVariableNames[Name]

    name_of_exists : ASSUMPTION
      EXISTS (f : [p : Program -> [Method -> (legal_names(p))]]) :
        FORALL(p : Program) :
          injective?(f(p))


  ENDASSUMING


  IMPORTING InlineProgramAnnotation[Name],
            AnnotatedProgramSemantics[Name]

  p : VAR Program

  % For every class in inlined program, a corresponding class in
  % original program exists

  inline_program_class_correspondence : LEMMA % :-)
    FORALL(c : Class) : classes(inline_program(p))(c) IMPLIES
      EXISTS (c1 : Class) : classes(p)(c1) AND c = inline_class(p)(c1)

  % For every method in inlined program, a corresponding method in
  % original program exists
  inline_program_method_correspondence : LEMMA % :-)
    FORALL(c : Class, m : Method) :
      classes(inline_program(p))(c) IMPLIES
      methods(c)(m) IMPLIES
        EXISTS (c1 : Class, m1 : Method) :
          classes(p)(c1) AND methods(c1)(m1) AND m = inline_method(p)(m1)

  % Field declarations unchanged by inlining
  inline_program_preserves_Field_decls : LEMMA % :-)
    getFieldDecls(inline_program(p)) = getFieldDecls(p) 

  % Ghost declarations unchanged by inlining
  inline_program_preserves_Ghost_decls : LEMMA % :-)
    getGhostDecls(inline_program(p)) = getGhostDecls(p)

  % Local variable declarations in inlined program union of  
  % local variable declarations in original program and new local variable
  % declarations
  inline_program_add_LocalVar_decls : LEMMA % :-)
    getLocalDecls(inline_program(p)) = union(getLocalDecls(p), new_lvars(p))

  % variable declarations in inlined program union of original program
  % declarations and new local variable declarations
  inline_program_ext_declared_var_names : LEMMA % :-)
     ext_declared_var_names(inline_program(p)) =
       union(ext_declared_var_names(p), var_names(new_lvars(p)))

  % inlined method wellformed wrt inlined program
  inline_program_preserves_wf_method : LEMMA % :-)
    FORALL (m : Method) :
      methods(p)(m) IMPLIES
      wf_Method(inline_program(p))(inline_method(p)(m))

  % any body that is wellformed wrt the original program is wellformed
  % wrt the inlined program
  inline_program_preserves_wf_body : LEMMA % :-)
    FORALL (b : (wf_Body(p))) :
      wf_Body(inline_program(p))(b)

  % name of classes in inlined program unchanged
  get_class_inline_program : LEMMA % :-)
    FORALL (c1, c2 : Class, tgt : Val) :
      up?(get_class(p)(tgt)) IMPLIES
      up?(get_class(inline_program(p))(tgt)) IMPLIES
      c1 = down(get_class(p)(tgt)) IMPLIES
      c2 = down(get_class(inline_program(p))(tgt)) IMPLIES
        name(c1) = name(c2)

  % if c1 is class in original program, and c2 is class in inlined program
  % and names are the same, then c2 = inline_class(c1)
  same_class_name_inline_class_implies_same_class : LEMMA % :-)
    FORALL (c1, c2 : Class) :
      classes(p)(c1) IMPLIES
      classes(inline_program(p))(c2) IMPLIES
      name(c1) = name(c2) IMPLIES
        c2 = inline_class(p)(c1)

  % method names not changed by annotations
  same_method_name_inline_class_implies_same_method : LEMMA % :-)
    FORALL (c1, c2 : Class, m1, m2 : Method) :
      classes(p)(c1) IMPLIES
      c2 = inline_class(p)(c1) IMPLIES
      methods(c1)(m1) IMPLIES
      methods(c2)(m2) IMPLIES
      name(m1) = name(m2) IMPLIES
        m2 = inline_method(p)(m1)


  % lookup method returns inlined method 
  lookup_mthd_inline_program : LEMMA % :-)
    FORALL (m1, m2 : Method, mn : Name, tgt : Val) :
      up?(lookup_mthd(p, tgt, mn)) IMPLIES
      up?(lookup_mthd(inline_program(p), tgt, mn)) IMPLIES
      m1 = down(lookup_mthd(p, tgt, mn)) IMPLIES
      m2 = down(lookup_mthd(inline_program(p), tgt, mn)) IMPLIES
        m2 = inline_method(p)(m1)

  % class names unchanged by annotation inlining
  inline_program_preserves_class_names : LEMMA % :-)
    class_names(p) = class_names(inline_program(p))

  % if a reference to class in original program, then also in inlined program
  up_get_class_equivalence : LEMMA % :-)
    FORALL(a : Val) :
      up?(get_class(p)(a)) IMPLIES
        up?(get_class(inline_program(p))(a))

  % get_class in inlined program returns inlined class
  get_class_equivalence : LEMMA % :-)
    FORALL(a : Val) :
      up?(get_class(p)(a)) IMPLIES
      get_class(inline_program(p))(a) =
        up(inline_class(p)(down(get_class(p)(a))))


END InlineProgramAnnotationProperties
