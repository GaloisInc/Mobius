MoreAnnotatedProgramProperties[CP, Name : TYPE+] : THEORY
BEGIN

  ASSUMING

    CP_is_enumerable : ASSUMPTION
      EXISTS (f : [CP -> nat]) : injective?(f)

    infinite_namespace : ASSUMPTION
      is_infinite({n : Name | TRUE})

    IMPORTING VariableNames[CP, Name]

    name_of_exists : ASSUMPTION
      EXISTS (f : [mp : (wf_MP) -> [CP -> (legal_names(mp))]]) : 
        FORALL(mp : (wf_MP)) : 
          injective?(f(mp))

  ENDASSUMING

  IMPORTING Equivalence[CP, Name]

  make_MPState(mp : (wf_MP))(s : APState) : MPState =
    (# astate := initial_MVA_state(mva(mp)),
       pstate := pstate(s),
       gvs := gvs(s) #)


  IMPORTING ContainsNoMethodCall[Name]

  derive_equivalence : LEMMA % :-)
    FORALL(mp : (wf_MP), b : (wf_Body(program(mp))),
           sAP1, sAP2 : APState, v : Val, n : nat) :
      contains_no_method_call(program(mp))(b) IMPLIES
      subset?(var_names(b), ext_declared_var_names(program(mp))) IMPLIES
      derive(ann_program(mp))(b, sAP1, v, sAP2)(n) IMPLIES
        derive(mp)(b, make_MPState(mp)(sAP1), v, make_MPState(mp)(sAP2))(n)

  contains_no_method_call_preserved_by_ann_program : LEMMA
    FORALL(mp : (wf_MP)) :
      contains_no_method_call_in_sets_in_program(program(mp)) IMPLIES
      contains_no_method_call_in_sets_in_program(ann_program(mp))


  IMPORTING OnlyJMLExceptionInSet[Name, Program, APState, id, 
                             LAMBDA(s : APState) : pstate(s), lookup,
                             update_PState, execute_set, execute_assert,
                             execute_CaseJML, on_method_entry,
                             on_method_exit_normal, 
                             on_method_exit_exceptional],
            OnlyJMLExceptionInSet[Name, MonitoredProgram, MPState,
                             LAMBDA(mp : MonitoredProgram) : program(mp),
                             LAMBDA(s : MPState) : pstate(s),
                             lookup, update_PState, execute_set,
                             execute_assert, execute_CaseJML, 
                             on_method_entry_MP, on_method_exit_normal_MP,
                             on_method_exit_exceptional_MP]

  only_JMLExc_in_sets_preserved_by_ann_program : LEMMA
    FORALL(mp : (wf_MP)) :
      only_JMLExc_in_sets_in_program(mp) IMPLIES
      actions_have_no_side_effects(mp) IMPLIES
      only_JMLExc_in_sets_in_program(ann_program(mp))


END MoreAnnotatedProgramProperties

