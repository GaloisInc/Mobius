EventType : DATATYPE
BEGIN

  Entry : Entry?
  ExitNormal : ExitNormal?
  ExitExceptional : ExitExceptional?

END EventType


% Multi Variable Automata
MVA[CP, Name : TYPE+] : THEORY
BEGIN

  IMPORTING JType,
            Expr[Name],
            Val

  
  MVA_VarDecl : TYPE =
    [# name : Name,
       jtype : JType,
       ival : Expr[Name]
    #]

  Prog_VarDecl : TYPE =
    [# name : Name,
       jtype : JType
    #]

  Store : TYPE = [Name -> Val]

  IMPORTING State[Name]

  Guard : TYPE = [Store, PSt -> boolean]

  OneAction : TYPE =
    [# target : Name,
       source : Expr[Name]
    #]

  Action : TYPE = list[OneAction]

  skip : Action = null[OneAction]

  IMPORTING EventType

  Event : TYPE =
    [# EType : EventType,
       mname : Name
    #]

  Transition : TYPE =
    [# scp : CP,         % source control point
       event : Event,
       guard : Guard,
       action : Action,
       tcp : CP          % target control point
    #]
   
  % halted is our characterization of a state where the automaton is blocked
  halted : CP

  % An NVA consists of:
  % a name of the class that the automaton monitors,
  % a finite set of control points other than halted,
  % an initial control point,
  % a finite set of events,
  % a finite set of variable declarations used by the automaton,
  % a finite set of vars decls from the program that may be used in guards,
  % a finite transition relation
  MVA : TYPE =
    [# name : Name,
       classname : Name,
       cps : finite_set[CP],
       init_cp : CP,
       evs : finite_set[Event],
       vdsA : finite_set[MVA_VarDecl],
       vdsP : finite_set[Prog_VarDecl],
       ts : finite_set[Transition]
    #]

  % NOTE: vdsA and vdsP are just the signatures of the variables, they do not
  %       contain their values. That is done by its (operational) semantics.

  % Functions to get a set of names from a set of variable declarations
  var_names(V : set[MVA_VarDecl]) : set[Name] =
    {n : Name | EXISTS (va : MVA_VarDecl) : V(va) AND name(va) = n}

  var_names(V : set[Prog_VarDecl]) : set[Name] =
    {n : Name | EXISTS (vp : Prog_VarDecl) : V(vp) AND name(vp) = n}

  % Gets the variable' names involved in an expression
  % TODO: define!
  var_names(e : Expr[Name]) : set[Name]

  valid_Store(vdsA : set[MVA_VarDecl])(f : Store) : boolean =
    FORALL (n : Name) :
      var_names(vdsA)(n) IMPLIES
        NOT Bottom?(f(n))

  valid_Store(f : Store, a : MVA) : boolean =
    valid_Store(vdsA(a))(f)

  valid_PSt(vdsP : set[Prog_VarDecl])(f : PSt) : boolean =
    FORALL (n : Name) :
      var_names(vdsP)(n) IMPLIES
        NOT Bottom?(lookup(n, f))

  valid_PSt(f : PSt, a : MVA) : boolean =
    valid_PSt(vdsP(a))(f)

  IMPORTING func_props   % imports domain_set

  valid_Guard(p : Guard, a : MVA) : boolean =
    valid_Store(proj_1(domain_set(p)), a) AND
    valid_PSt(proj_2(domain_set(p)), a)

  valid_Action(l : Action, a : MVA) : boolean =
    FORALL (c : OneAction) :
      member(c, l) IMPLIES
        var_names(vdsA(a))(target(c)) AND
        subset?(var_names(source(c)), 
                union(var_names(vdsA(a)), var_names(vdsP(a))))

  % Rules that every MVA must follow:
  % The initial control point is in the set of control points of the automaton
  % The name of the variables in vdsA and vdsP are distinct
  % The source and target of every transition is in the set of control points
  % The event of every transition is in the set of events
  % The target of every OneAction in every transition is in vdsA and variables
  % appearing in the source expression are either in vdsA or in vdsP
  % Variables appearing in guards are in in vdsA or in vdsP. In our formalization
  % this means that the Store is defined for each var in vdsA and the PSt is
  % defined for each var in vdsP (they also may be defined elsewhere)
  valid_MVA(a : MVA) : boolean =
    cps(a)(init_cp(a)) AND
    disjoint?(var_names(vdsA(a)), var_names(vdsP(a))) AND
    FORALL (t : Transition) :
      ts(a)(t) IMPLIES
        cps(a)(scp(t)) AND
        cps(a)(tcp(t)) AND
        evs(a)(event(t)) AND
        valid_Guard(guard(t), a) AND
        valid_Action(action(t), a)

  deterministic(a : (valid_MVA)) : boolean =
    FORALL (t1, t2 : Transition, stA : Store, stP : PSt) :
      ts(a)(t1) IMPLIES
      ts(a)(t2) IMPLIES
      valid_Store(stA, a) IMPLIES
      valid_PSt(stP, a) IMPLIES
      scp(t2) = scp(t1) IMPLIES
      event(t2) = event(t1) IMPLIES
      guard(t1)(stA, stP) IMPLIES
      guard(t2)(stA, stP) IMPLIES
        t1 = t2

  total(a : (valid_MVA)) : boolean =
    FORALL (q : CP, e : Event, stA : Store, stP : PSt) :
      cps(a)(q) IMPLIES
      evs(a)(e) IMPLIES
      valid_Store(stA, a) IMPLIES
      valid_PSt(stP, a) IMPLIES
        EXISTS (t : Transition) : ts(a)(t) AND
                                  scp(t) = q AND
                                  event(t) = e AND
                                  guard(t)(stA, stP)

  trap_state(q : CP, a : (valid_MVA)) : boolean =
    FORALL (t : Transition) :
      ts(a)(t) AND scp(t) = q IMPLIES
        tcp(t) = halted

  complete(a : (valid_MVA)) : boolean =
    cps(a)(halted) AND
    trap_state(halted, a) AND
    deterministic(a) AND
    total(a)

END MVA


MVA_Semantics[CP, Name : TYPE+] : THEORY
BEGIN

  IMPORTING MVA[CP, Name]

  % The state of a MVA is composed by its currrent control point and values for
  % its variables
  AState : TYPE =
    [# cp : CP,
       stA : Store 
    #]

  valid_AState(s : AState, a : MVA) : boolean =
    cps(a)(cp(s)) AND
    valid_Store(stA(s), a)

  valid_AState(a : MVA)(s : AState) : boolean =
    valid_AState(s, a)

  % A program event is an event that occurred in the program together with the 
  % value of its variables at that point
  PEvent : TYPE =
    [# stP : PSt,
       event : Event
    #]

  valid_PEvent(pe : PEvent, a : MVA) : boolean =
    evs(a)(event(pe)) AND valid_PSt(stP(pe), a)

  valid_PEvent(a : MVA)(pe : PEvent) : boolean =
    valid_PEvent(pe, a)

  % Updates a store
  update(st : Store)(n : Name)(v : Val) : Store =
    LAMBDA(m : Name) :
      IF m = n
      THEN v
      ELSE st(m)
      ENDIF

  % TODO: define!
  eval(e : Expr)(st : Store) : Val

  % type checking problem, because program expressions cannot be evaluated
  execute_actions(stA : Store, l : Action) : RECURSIVE Store =
    IF null?(l)
    THEN stA
    ELSE execute_actions(update(stA)(target(car(l)))
                                    (eval(source(car(l)))(stA)), cdr(l))
    ENDIF
  MEASURE length(l)

  % Single judgement for a MVA
  step(a : (total))(x : (valid_AState(a)), p : (valid_PEvent(a))) : AState =
      LET t = choose(LAMBDA (t : Transition) : 
                       ts(a)(t) AND 
                       scp(t) = cp(x) AND 
                       event(t) = event(p) AND
                       guard(t)(stA(x), stP(p))) IN
      (# cp := tcp(t),
         stA := execute_actions(stA(x), action(t))
      #)


  %TODO: prove (needs the definition of eval for Expressions)
  execute_action_yields_valid_state: LEMMA
    FORALL (a : MVA, stA : Store, l : Action) :
      valid_Store(stA, a) IMPLIES
        valid_Store(execute_actions(stA, l), a)

  step_yields_valid_state : THEOREM
    FORALL (a : (total), x : (valid_AState(a)), p : (valid_PEvent(a))):
      valid_AState(step(a)(x, p), a) 
   
END MVA_Semantics


MVA_Completion[CP, Name : TYPE+] : THEORY
BEGIN

  IMPORTING MVA[CP, Name], Epsilon_Props, MVA_Finite_Props

  % Returns the set of guards from transitions in ts with cp as source control
  % point and e as event
  all_guards(cp : CP, e : Event, 
             ts : finite_set[Transition]) : finite_set[Guard] =
    {p : Guard | EXISTS (t : Transition) : ts(t) AND scp(t) = cp AND
                                           event(t) = e AND guard(t) = p}
 
  % Returns a guard that is the conjuction of the negation of the guards in ts
  % with source control point cp and event e. The sets of variable declarations
  % are needed to make sure that the guard is defined in for those variables.
  build_guard(cp : CP, e : Event, ts : finite_set[Transition], 
              vdsA : finite_set[MVA_VarDecl], 
              vdsP : finite_set[Prog_VarDecl]) : Guard = 
    LAMBDA (stA : (valid_Store(vdsA)), stP : (valid_PSt(vdsP))) :
      FORALL (p : Guard) : all_guards(cp, e, ts)(p) IMPLIES NOT p(stA, stP)

  % Lemmas to prove the TCC2
  MC_TCC2_subset1_is_finite: LEMMA
    FORALL (cps: finite_set[CP], es: finite_set[Event[CP, Name]],
            ts: finite_set[Transition[CP, Name]], vdsA : finite_set[MVA_VarDecl],
            vdsP : finite_set[Prog_VarDecl]) :
      is_finite({t: Transition[CP, Name] |
                    cps(scp(t)) AND es(event(t)) AND
                    action(t) = skip[CP, Name] AND
                    tcp(t) = halted[CP, Name] AND
                    guard(t) = build_guard(scp(t), event(t), ts, vdsA, vdsP)})

  % Makes ts complete with respect cps and es
  completeTrans(cps : finite_set[CP], es : finite_set[Event], 
                ts : finite_set[Transition], vdsA : finite_set[MVA_VarDecl],
                vdsP : finite_set[Prog_VarDecl]) : finite_set[Transition] =
      union({t : Transition | cps(scp(t)) AND es(event(t)) AND
                              action(t) = skip AND tcp(t) = halted AND
                              guard(t) = build_guard(scp(t), event(t), ts, vdsA, 
                                                     vdsP)},
            {t : Transition | scp(t) = halted AND es(event(t)) AND
                              guard(t) = (LAMBDA (stA : (valid_Store(vdsA)) , 
                                                  stP : (valid_PSt(vdsP))): TRUE)
                              AND action(t) = skip AND tcp(t) = halted})

  % Preconditions for completeMVA
  completeMVA_pre(a : (valid_MVA)) : boolean =
    deterministic(a) AND NOT cps(a)(halted)

  % Makes a deterministic MVA total
  completeMVA(a : (completeMVA_pre)) : MVA =
    a WITH [cps := add(halted, cps(a)),
            ts := union(ts(a), completeTrans(cps(a), evs(a), ts(a), vdsA(a), 
                                             vdsP(a)))]

  %    Lemmas to prove completeMVA_is_complete
  % TODO: prove (may be long)
  completeMVA_valid: LEMMA
    FORALL (a: (completeMVA_pre)) :
      valid_MVA(completeMVA(a))

  completeMVA_trap: LEMMA
    FORALL (a: (completeMVA_pre)) :
      trap_state(halted, completeMVA(a))

  completeMVA_determ: LEMMA
    FORALL (a: (completeMVA_pre)) :
      deterministic(completeMVA(a))

  completeMVA_total: LEMMA
    FORALL (a: (completeMVA_pre)) :
      total(completeMVA(a))

  completeMVA_is_complete: THEOREM
    FORALL (a: (completeMVA_pre)) :
      complete(completeMVA(a))

END MVA_Completion
