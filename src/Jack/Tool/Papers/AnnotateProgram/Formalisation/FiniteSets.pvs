% NOTE: This theory is not used but the lemma may be interesting
FiniteSetProps[X, Y : TYPE] : THEORY
BEGIN

  get_y_from_x(P: pred[[X, Y]], x: {x: X | EXISTS (y: Y) : P(x, y)}): Y = 
    choose({y: Y | P(x, y)})

  subset_prop: LEMMA % :-)
    FORALL (P: pred[[X, Y]]):
      is_finite(P) IMPLIES
        is_finite({x: X | EXISTS (y: Y) : P(x, y)})

END FiniteSetProps


EpsilonProps[T : TYPE+] : THEORY
BEGIN

  epsilon_singleton: LEMMA % :-)
    FORALL (x : T):
      epsilon(singleton(x)) = x

END EpsilonProps


% Lemmas needed to prove the TCCS in MVACompletion
MVAFiniteProps[CP, Name : TYPE+] : THEORY
BEGIN

  IMPORTING MVA[CP, Name], 
            EpsilonProps

  % Lemmas to prove all_guards_TCC1 from MVA_Completion
  get_t_from_gs(ts: finite_set[Transition],
                gs: {p: Guard[CP, Name] | EXISTS (t: Transition[CP, Name]): ts(t)
                                                  AND t`guard = p}): Transition =
    choose({t: Transition | ts(t) AND gs(t`guard)})

  all_guards_finite: LEMMA % :-)
    FORALL (ts: finite_set[Transition[CP, Name]]):
      is_finite({p: Guard[CP, Name] | EXISTS (t: Transition[CP, Name]):
        ts(t) AND t`guard = p})

  all_guards_subset: LEMMA % :-)
    FORALL (cp: CP, e: Event[CP, Name], ts: finite_set[Transition[CP, Name]]):
      subset?(gs, all_guards)
      WHERE
        gs = {p: Guard[CP, Name] | EXISTS (t: Transition[CP, Name]):
              ts(t) AND t`scp = cp AND t`event = e AND t`guard = p},
        all_guards = {p: Guard[CP, Name] | EXISTS (t: Transition[CP, Name]):
                      ts(t) AND t`guard = p}

  % TODO: The TCC has changed!
  % Lemma to prove complete_trans_TCC1 from MVA_Completion
  complete_trans_TCC1_subset2_is_finite: LEMMA % :-)
     FORALL (es: finite_set[Event[CP, Name]]):
       is_finite({t: Transition[CP, Name] |
                    scp(t) = halted[CP, Name] AND
                    es(event(t)) AND
                    guard(t) =
                      (LAMBDA (stA: Store[CP, Name], stP: PSt[Name]): TRUE) AND
                    action(t) = skip[CP, Name] AND
                    tcp(t) = halted[CP, Name]})


END MVAFiniteProps
